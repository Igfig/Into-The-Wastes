# make sure this script loads second (after add_biomes.cfg)
# if it's any earlier, the WildNature biomes won't have been added yet
Tweaker.setScriptStage("POST_INIT")

# mark PLANAR biomes
planarBiomes = forBiomes("minecraft:hell", "minecraft:sky", "minecraft:void", "rats:ratlantis", "arcaneworld:arcane_dungeon")
planarBiomes.addDicTypes("PLANAR")

# mark DESERT biomes
# yeah it's simplest to just manually specify which ones we want. (well I have a python script that does it so it's technically not manual, but for our purposes here it is)

naturalDeserts = forBiomes("biometweaker:villageless_desert", "biomesoplenty:quagmire", "biomesoplenty:volcanic_island", "biomesoplenty:wasteland", "environs:canyon", "environs:stone_basin", "environs:stone_hills", "environs:wasteland_spikes", "environs:wasteland_eroded", "minecraft:desert", "minecraft:mesa", "minecraft:mesa_clear_rock", "minecraft:mesa_rock", "minecraft:mutated_desert", "minecraft:mutated_mesa", "minecraft:mutated_mesa_clear_rock", "minecraft:mutated_mesa_rock", "minecraft:ocean", "traverse:canyon", "traverse:red_desert", "valoegheses_be:barelands", "valoegheses_be:red sand dunes", "valoegheses_be:sand dunes", "valoegheses_be:wasteland flats", "valoegheses_be:wasteland flats slopes", "wildnature:desert_desolation", "wildnature:dunes", "wildnature:grand_canyon", "wildnature:light_mesa", "wildnature:mixed_desert", "wildnature:mixed_desert_mountains", "wildnature:volcano")

# technical biomes that are still deserts
technicalDeserts = forBiomes("minecraft:river", "minecraft:cold_beach", "minecraft:desert_hills", "valoegheses_be:wasteland flats trees", "minecraft:deep_ocean", "minecraft:stone_beach", "minecraft:beaches", "minecraft:frozen_ocean", "minecraft:frozen_river")

# these last few don't actually show up in the world, but are included for completeness
unusedDeserts = forBiomes("valoegheses_be:sand dunes m")

desertBiomes = forBiomes(naturalDeserts, technicalDeserts, unusedDeserts)

desertBiomes.addDicTypes("DESERT") # custom biome dict
desertBiomes.removeDicTypes("HOT", "WARM", "COOL", "COLD", "HILLS", "JUNGLE", "MOUNTAIN", "OCEAN", "PLAINS", "SNOWY", "SWAMP", "WATER", "WET") # we want to be able to set mobs and ores to spawn by biome dict, but keep them from spawning in the desert unless explicitly told to

nonDesertBiomes = forAllBiomesExcept(desertBiomes, planarBiomes)
nonDesertBiomes.addDicTypes("OASIS") # custom biome dict


# mark OCEAN biomes
oceanBiomes = forBiomes("biomesoplenty:coral_reef", "biomesoplenty:kelp_forest", "valoegheses_be:archipelago", "valoegheses_be:stony reef", "wildnature:tropical_water_rocks")
oceanBiomes.addDicTypes("OCEAN")

# mark some sufficently watery biomes as WATER
wateryBiomes = forBiomesOfTypes("BEACH", "SWAMP")
wateryOases = intersectionOf(wateryBiomes, nonDesertBiomes) # extra step for safety
wateryOases.addDicTypes("WATER")

moreWateryBiomes = forBiomes("traverse:mini_jungle", "wildnature:overgrown_mountains", "valoegheses_be:bluff", "environs:floating_end_forest")
moreWateryBiomes.addDicTypes("WATER")


# mark HOT biomes

naturalHot = forBiomes("environs:canyon", "environs:dry_oak_forest", "environs:dry_oak_forest_m", "environs:sandstone_ranges", "minecraft:mutated_savanna", "minecraft:mutated_savanna_rock", "minecraft:savanna", "minecraft:savanna_rock", "traverse:badlands", "traverse:desert_shrubland", "valoegheses_be:dense brushlands", "wildnature:mixed_savanna_desert", "wildnature:mixed_savanna_desert_hills", "wildnature:outback", "wildnature:red_savanna_desert", "wildnature:red_savanna_desert_hills", "wildnature:savanna_desert", "wildnature:savanna_desert_edge", "wildnature:savanna_desert_edge_hills", "wildnature:savanna_desert_hills", "wildnature:steppe")

technicalHot = forBiomes("minecraft:mushroom_island_shore",  "minecraft:mushroom_island")

unusedHot = forBiomes("valoegheses_be:hot brushlands", "valoegheses_be:australian outback", "valoegheses_be:wasteland flats trees")

hotBiomes = forBiomes(naturalHot, technicalHot, unusedHot)

hotBiomes.addDicTypes("HOT")
hotBiomes.removeDicTypes("WARM", "COOL", "COLD")


# mark WARM biomes

naturalWarm = forBiomes("biomesoplenty:bamboo_forest", "biomesoplenty:lavender_fields", "biomesoplenty:mystic_grove", "biomesoplenty:oasis", "biomesoplenty:overgrown_cliffs", "environs:kakadu", "environs:lush_forest", "environs:overgrown_plains", "environs:silk_wetland", "environs:tall_oak_forest", "environs:tall_oak_forest_m", "minecraft:birch_forest", "minecraft:birch_forest_hills", "minecraft:forest", "minecraft:jungle", "minecraft:mutated_birch_forest", "minecraft:mutated_birch_forest_hills", "minecraft:mutated_forest", "minecraft:mutated_jungle", "minecraft:mutated_plains", "minecraft:plains", "traverse:lush_hills", "traverse:thicket", "valoegheses_be:ghost forest", "valoegheses_be:orchid fields", "valoegheses_be:tropical jungle", "wildnature:dark_dense_forest", "wildnature:deciduous_forest", "wildnature:dense_forest", "wildnature:field_of_corn", "wildnature:flower_field", "wildnature:grassland", "wildnature:jacaranda_forest", "wildnature:madagascar_pasture", "wildnature:madagascar_rainforest", "wildnature:mahogany_mountains", "wildnature:mahogany_rainforest", "wildnature:masuria", "wildnature:natural_maze", "wildnature:orchard", "wildnature:prairie", "wildnature:prairie_plateau", "wildnature:purple_field", "wildnature:tropical_mesa", "wildnature:tropics")

technicalWarm = forBiomes("minecraft:forest_hills", "minecraft:jungle_edge", "minecraft:jungle_hills", "minecraft:mutated_jungle_edge")

unusedWarm = forBiomes("valoegheses_be:brushlands hills", "valoegheses_be:brushlands", "valoegheses_be:bush", "valoegheses_be:bushland hills", "valoegheses_be:forested canyon pillars", "valoegheses_be:forested canyon plateau m", "valoegheses_be:forested canyon plateau", "valoegheses_be:high inhabited woodlands", "valoegheses_be:high woodlands", "valoegheses_be:low tall woodlands", "valoegheses_be:low woodlands", "valoegheses_be:lowlands chapparal", "valoegheses_be:lowlands", "valoegheses_be:woodlands hills", "wildnature:eucalyptus_forest")

warmBiomes = forBiomes(naturalWarm, technicalWarm, unusedWarm)

warmBiomes.addDicTypes("WARM")
warmBiomes.removeDicTypes("HOT", "COOL", "COLD")


# mark COOL biomes

naturalCool = forBiomes("biomesoplenty:bayou", "biomesoplenty:crag", "biomesoplenty:mangrove", "biomesoplenty:lush_desert", "biomesoplenty:ominous_woods", "biomesoplenty:shield", "environs:emerald_cliffs", "environs:mushroom_rainforest", "environs:pine_taiga", "environs:pine_taiga_m", "environs:roofed_swamp", "environs:tall_oak_wetland", "minecraft:extreme_hills", "minecraft:extreme_hills_with_trees", "minecraft:mutated_extreme_hills", "minecraft:mutated_extreme_hills_with_trees", "minecraft:mutated_redwood_taiga", "minecraft:mutated_roofed_forest", "minecraft:mutated_swampland", "minecraft:redwood_taiga", "minecraft:redwood_taiga_hills", "minecraft:roofed_forest", "minecraft:swampland", "traverse:crag_cliffs", "traverse:mini_jungle", "traverse:rocky_plains", "traverse:temperate_rainforest", "valoegheses_be:bluff", "valoegheses_be:highlands", "valoegheses_be:mire", "valoegheses_be:moorlands", "wildnature:amazonia", "wildnature:autumnal_forest", "wildnature:bialowieza_forest", "wildnature:birch_grove", "wildnature:blue_plains", "wildnature:boreal_pine_forest", "wildnature:cedar_forest", "wildnature:cherry_orchard", "wildnature:coniferous_forest", "wildnature:deciduous_mountain", "wildnature:diorite_mountains", "wildnature:granite_mountains", "wildnature:highlands", "wildnature:hornbeam_forest", "wildnature:land_of_rivers", "wildnature:lupine_field", "wildnature:maple_taiga", "wildnature:misty_swamp", "wildnature:overgrown_mountains", "wildnature:peatlands", "wildnature:rainforest", "wildnature:redwood_forest", "wildnature:rocky_mountains", "wildnature:roofed_forest_hills", "wildnature:sandy_tropics", "wildnature:sea_highlands", "wildnature:taiga_mountains", "wildnature:taiga_plateau", "wildnature:tropical_water_rocks", "wildnature:tundra", "wildnature:wetlands")

technicalCool = forBiomes("minecraft:mutated_redwood_taiga_hills", "minecraft:mutated_taiga", "minecraft:smaller_extreme_hills", "minecraft:taiga", "minecraft:taiga_hills")

unusedCool = forBiomes("valoegheses_be:grassy fen", "valoegheses_be:grassy marshland", "valoegheses_be:low spruce woodlands", "valoegheses_be:northern subalpine alps", "valoegheses_be:sand dunes oasis", "valoegheses_be:southern alps subalpine", "valoegheses_be:wasteland flats oasis", "wildnature:marsh", "wildnature:pine_forest")

coolBiomes = forBiomes(naturalCool, technicalCool, unusedCool)

coolBiomes.addDicTypes("COOL")
coolBiomes.removeDicTypes("HOT", "WARM", "COLD")


# mark COLD biomes

naturalCold = forBiomes("biomesoplenty:coral_reef", "biomesoplenty:kelp_forest", "biomesoplenty:sacred_springs", "environs:cold_pine_taiga", "environs:cold_pine_taiga_m", "environs:icy_hills", "iceandfire:glacier", "minecraft:ice_flats", "minecraft:ice_mountains", "minecraft:mutated_ice_flats", "minecraft:mutated_taiga_cold", "minecraft:taiga_cold", "minecraft:taiga_cold_hills", "traverse:glacier_spikes", "valoegheses_be:archipelago", "valoegheses_be:northern snow alps", "valoegheses_be:northern snow subalpine alps", "valoegheses_be:snow rocks", "wildnature:frozen_coniferous_forest", "wildnature:frozen_mountains", "wildnature:glacier", "wildnature:high_plains", "wildnature:himalayas", "wildnature:misty_mountains", "wildnature:pine_hill", "wildnature:polar_plateau", "wildnature:prismarine_mountains", "wildnature:rocky_spruce_forest", "wildnature:snowy_bialowieza_forest", "wildnature:snowy_coniferous_forest", "wildnature:tatra_mountains")

unusedCold = forBiomes("valoegheses_be:extreme southern alps", "valoegheses_be:northern alps", "valoegheses_be:snow rocks mountains", "valoegheses_be:snow rocks plateau", "valoegheses_be:southern alps", "valoegheses_be:white oaks hills", "valoegheses_be:white oaks", "valoegheses_be:white woods hills", "valoegheses_be:white woods")

coldBiomes = forBiomes(naturalCold, unusedCold)

coldBiomes.addDicTypes("COLD")
coldBiomes.removeDicTypes("HOT", "WARM", "COOL")


# give swamps RIVER for the sake of the river mobs who can't go in desert rivers
swampBiomes = forBiomesOfTypes("SWAMP")
swampOasisBiomes = intersectionOf(swampBiomes, nonDesertBiomes)
swampOasisBiomes.addDicTypes("RIVER")

moreRiverBiomes = forBiomes("biomesoplenty:shield", "wildnature:land_of_rivers")
moreRiverBiomes.addDicTypes("RIVER")

# mark volcanos as DEAD
volcanoBiomes = forBiomes("biomesoplenty:volcanic_island", "wildnature:volcano")
volcanoBiomes.addDicTypes("DEAD")

# mark some wastelands as WASTELAND
wastelandBiomes = forBiomes("environs:wasteland_spikes", "valoegheses_be:wasteland flats")
wastelandBiomes.addDicTypes("WASTELAND")

# some shouldn't be, though
quagmire = forBiomes("biomesoplenty:quagmire", "iceandfire:glacier")
quagmire.removeDicTypes("WASTELAND")

# mark beaches as SANDY
desertBeachBiomes = forBiomes("minecraft:beaches")
desertBeachBiomes.addDicTypes("SANDY")

# mark tropical water biomes as BEACH
beachBiomes = forBiomes("biomesoplenty:coral_reef", "biomesoplenty:mangrove", "valoegheses_be:archipelago","wildnature:sandy_tropics", "wildnature:tropical_water_rocks", "wildnature:tropics")
beachBiomes.addDicTypes("BEACH")

# mark some sufficently hilly biomes as HILLS
hillyBiomes = forBiomes("environs:sandstone_ranges", "wildnature:mahogany_mountains")
hillyBiomes.addDicTypes("HILLS")

# add HILLS to all HOT MOUNTAIN biomes
hotBiomes = forBiomesOfTypes("HOT")
mountainBiomes = forBiomesOfTypes("MOUNTAIN")
hotMountains = intersectionOf(hotBiomes, mountainBiomes)
hotMountains.addDicTypes("HILLS")

# remove MOUNTAIN and PLAINS from all HOT biomes (they should be HILLS and SAVANNA respectively)
hotBiomes.removeDicTypes("MOUNTAIN")
hotBiomes.removeDicTypes("PLAINS")

# add MOUNTAIN to some biomes that do deserve it
newMountainBiomes = forBiomes("wildnature:overgrown_mountains", "valoegheses_be:extreme southern alps", "iceandfire:glacier", "wildnature:rocky_spruce_forest", "valoegheses_be:southern alps")
newMountainBiomes.addDicTypes("MOUNTAIN")

# mark some biomes as SPOOKY
spookyBiomes = forBiomes("valoegheses_be:ghost forest", "environs:silk_wetland", "environs:end_forest", "environs:floating_end_forest")
spookyBiomes.addDicTypes("SPOOKY")

# remove PLAINS from all SAVANNA biomes
savannaBiomes = forBiomesOfTypes("SAVANNA")
savannaBiomes.removeDicTypes("PLAINS")

# remove PLAINS and FOREST and SAVANNA from all DEAD and WASTELAND biomes
deadBiomes = forBiomesOfTypes("DEAD", "WASTELAND")
deadBiomes.removeDicTypes("FOREST", "PLAINS", "SAVANNA")

# crags and other desolate mountains spawn like regular biomes, but they count as deserts
crag = forBiomes("biomesoplenty:crag", "traverse:crag_cliffs")
crag.removeDicTypes("OASIS")
crag.addDicTypes("DESERT")

# re-remove unwanted biome types from deserts, because the commands don't actually seem to be properly sequential per se
desertBiomes.removeDicTypes("HOT", "WARM", "COOL", "COLD", "HILLS", "JUNGLE", "MOUNTAIN", "OCEAN", "PLAINS", "SNOWY", "SPARSE", "SWAMP", "WATER", "WET")

# add missing biome types for some special biomes
ratlantis = forBiomes("rats:ratlantis")
ratlantis.addDicTypes("DENSE", "JUNGLE", "WET", "WARM")
void = forBiomes("arcaneworld:arcane_void", "arcaneworld:arcane_dungeon")
ratlantis.addDicTypes("VOID")